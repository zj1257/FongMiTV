plugins {
    id 'com.android.application'
}

android {
    namespace 'com.fongmi.android.tv'

    compileSdk 35
    flavorDimensions = ["mode", "api", "abi"]

    defaultConfig {
        applicationId "com.fongmi.android.tv"
        minSdk 21
        //noinspection ExpiredTargetSdkVersion
        targetSdk 28
        versionCode 285
        versionName "2.8.5"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString(), "eventBusIndex": "com.fongmi.android.tv.event.EventIndex"]
            }
        }
    }

    productFlavors {
        leanback {
            dimension "mode"
        }
        mobile {
            dimension "mode"
        }
        java {
            dimension "api"
        }
        python {
            dimension "api"
        }
        x86 {
            dimension "abi"
            ndk { abiFilters "x86" }
        }
        arm64_v8a {
            dimension "abi"
            ndk { abiFilters "arm64-v8a" }
        }
        armeabi_v7a {
            dimension "abi"
            ndk { abiFilters "armeabi-v7a" }
        }
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/beans.xml']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            outputFileName = "${variant.productFlavors[0].name}-${variant.productFlavors[1].name}-${variant.productFlavors[2].name}.apk"
        }
    }

    configurations.configureEach {
        resolutionStrategy {
            force 'com.squareup.okhttp3:okhttp:' + okhttpVersion
        }
    }

    lint {
        disable 'UnsafeOptInUsageError'
    }
}

dependencies {
    implementation project(':catvod')
    implementation project(':quickjs')
    pythonImplementation project(':chaquo')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.media:media:1.7.0'
    implementation 'androidx.media3:media3-common:' + media3Version
    implementation 'androidx.media3:media3-container:' + media3Version
    implementation 'androidx.media3:media3-database:' + media3Version
    implementation 'androidx.media3:media3-datasource:' + media3Version
    implementation 'androidx.media3:media3-datasource-okhttp:' + media3Version
    implementation 'androidx.media3:media3-datasource-rtmp:' + media3Version
    implementation 'androidx.media3:media3-decoder:' + media3Version
    implementation 'androidx.media3:media3-exoplayer:' + media3Version
    implementation 'androidx.media3:media3-exoplayer-dash:' + media3Version
    implementation 'androidx.media3:media3-exoplayer-hls:' + media3Version
    implementation 'androidx.media3:media3-exoplayer-rtsp:' + media3Version
    implementation 'androidx.media3:media3-exoplayer-smoothstreaming:' + media3Version
    implementation 'androidx.media3:media3-extractor:' + media3Version
    implementation 'androidx.media3:media3-ui:' + media3Version
    implementation 'androidx.room:room-runtime:2.6.1'
    implementation 'cat.ereza:customactivityoncrash:2.4.0'
    implementation 'com.github.bassaer:materialdesigncolors:1.0.0'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.github.bumptech.glide:annotations:4.16.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.16.0'
    implementation 'com.github.jahirfiquitiva:TextDrawable:1.0.3'
    implementation 'com.github.thegrizzlylabs:sardine-android:0.9'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.net.cronet:cronet-okhttp:0.1.0'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.guolindev.permissionx:permissionx:1.8.0'
    implementation 'com.hierynomus:smbj:0.14.0'
    implementation 'io.antmedia:rtmp-client:3.2.0'
    implementation 'javax.servlet:javax.servlet-api:3.1.0'
    implementation 'net.java.dev.jna:jna:5.2.0'
    implementation 'org.chromium.net:cronet-embedded:76.3809.111'
    implementation 'org.eclipse.jetty:jetty-client:8.1.22.v20160922'
    implementation('org.eclipse.jetty:jetty-server:8.1.22.v20160922') { exclude group: 'org.eclipse.jetty.orbit', module: 'javax.servlet' }
    implementation('org.eclipse.jetty:jetty-servlet:8.1.22.v20160922') { exclude group: 'org.eclipse.jetty.orbit', module: 'javax.servlet' }
    implementation 'org.fourthline.cling:cling-core:2.1.2-SNAPSHOT'
    implementation 'org.fourthline.cling:cling-support:2.1.2-SNAPSHOT'
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation('org.simpleframework:simple-xml:2.7.1') { exclude group: 'stax', module: 'stax-api' exclude group: 'xpp3', module: 'xpp3' }
    implementation(ext: 'aar', name: 'dlna-core-release', group: 'fongmi', version: 'release')
    implementation(ext: 'aar', name: 'dlna-dmc-release', group: 'fongmi', version: 'release')
    implementation(ext: 'aar', name: 'dlna-dmr-release', group: 'fongmi', version: 'release')
    implementation(ext: 'aar', name: 'forcetech-release', group: 'fongmi', version: 'release')
    implementation(ext: 'aar', name: 'hook-release', group: 'fongmi', version: 'release')
    implementation(ext: 'aar', name: 'jianpian-release', group: 'fongmi', version: 'release')
    implementation(ext: 'aar', name: 'nanohttpd-release', group: 'fongmi', version: 'release')
    implementation(ext: 'aar', name: 'thunder-release', group: 'fongmi', version: 'release')
    implementation(ext: 'aar', name: 'tvbus-release', group: 'fongmi', version: 'release')
    implementation(ext: 'aar', name: 'youtube-release', group: 'fongmi', version: 'release')
    implementation(ext: 'aar', name: 'zlive-release', group: 'fongmi', version: 'release')
    leanbackImplementation 'androidx.leanback:leanback:1.2.0-alpha04'
    leanbackImplementation 'com.github.JessYanCoding:AndroidAutoSize:1.2.1'
    mobileImplementation 'androidx.biometric:biometric:1.1.0'
    mobileImplementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    mobileImplementation 'com.google.android.flexbox:flexbox:3.0.0'
    mobileImplementation('com.journeyapps:zxing-android-embedded:4.3.0') { transitive = false }
    annotationProcessor 'androidx.room:room-compiler:2.6.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.3.1'
}